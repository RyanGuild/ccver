
WHITESPACE = _{ " " }
CONVENTIONAL_COMMIT = { HEADLINE ~ BODY_SECTION? ~ FOOTER_SECTION? }
COMMIT = { CONVENTIONAL_COMMIT | BODY }


TAG = {TYPE  ~ SCOPE_SECTION? ~ BREAKING_BANG }
HEADLINE = { TAG ~ ":" ~ DESCRIPTION }


TYPE = @{ LETTER+ }
SCOPE = @{ (LETTER | NUMBER | "-" | "_" | "/" | ".")+ }
SCOPE_SECTION = { "(" ~  SCOPE ~ ")" }
BREAKING_BANG = @{ "!"? }
TEXT = _{(LETTER | NUMBER | WHITESPACE | PUNCTUATION | SYMBOL)+}
DESCRIPTION = @{ TEXT+ }
BODY_SECTION = _{ NEWLINE ~ NEWLINE+ ~ BODY }
BODY = @{ (TEXT ~ NEWLINE)* ~ TEXT }
FOOTER_SECTION = _{ NEWLINE ~ NEWLINE+ ~ (FOOTER ~ NEWLINE)* ~ FOOTER }
FOOTER = {  SCOPE ~ (": " | " #") ~ DESCRIPTION }


COMMIT_HASHLINE = { SHA ~ NEWLINE }

PARENT_HASHLINE = { (SHA~" "?)* ~ NEWLINE }


SHA = @{ ('0'..'9' | 'a'..'f'){40} }
LOG_RAW_ENTRY = { 
    COMMIT_HASHLINE 
    ~ PARENT_HASHLINE
    ~ COMMIT
    ~ NEWLINE
}

LOG_RAW = { (LOG_RAW_ENTRY~NEWLINE?)+ ~ NEWLINE? }