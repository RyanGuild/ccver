
WHITESPACE = _{ " " | NEWLINE }
TAG = {TYPE  ~ SCOPE_SECTION? ~ BREAKING_BANG }
SUBJECT = { CONVENTIONAL_SUBJECT | DESCRIPTION }
CONVENTIONAL_SUBJECT = { TAG ~ ":" ~ DESCRIPTION }


TYPE = @{ LETTER+ }
SCOPE = @{ (LETTER | NUMBER | "-" | "_" | "/" | ".")+ }
FNAME = @{ (LETTER | NUMBER | "-" | "_" | ".")+}
NAME = @{ (LETTER | NUMBER | "-" | "_")+ }
SCOPE_SECTION = { "(" ~  SCOPE ~ ")" }
BREAKING_BANG = @{ "!"? }
TEXT = _{(LETTER | NUMBER | " " | PUNCTUATION | SYMBOL)+}
DESCRIPTION = @{ TEXT+ }

BODY_SECTION = { "{" ~ BODY ~ "}" }
FOOTER_SECTION = {  FOOTER* }

BODY = @{ (TEXT~NEWLINE?)*}
FOOTER = {  SCOPE ~ (": " | " #") ~ DESCRIPTION }


COMMIT_HASHLINE = { SHA }

PARENT_HASHLINE = { SHA* }

DECORATIONS_LINE = { "(" ~ (DECORATION~",")* ~ DECORATION? ~ ")"}
DECORATION = { HEAD_DEC | TAG_DEC | REMOTE_DEC | BRANCH_DEC }
HEAD_DEC = { "HEAD ->" ~ SCOPE }
TAG_DEC = { "tag: " ~ (CCVER_VERSION | SCOPE) }
REMOTE_DEC = { FNAME~"/"~SCOPE }
BRANCH_DEC = { SCOPE }


SHA = @{ ('0'..'9' | 'a'..'f'){40} }
SHORT_SHA = @{('0'..'9' | 'a'..'f'){7}}

ISO8601_DATE = @{ 
    YEAR ~ "-" ~ MONTH ~ "-" ~ DAY ~ "T" ~ HOUR ~ ":" ~ MINUTE ~ ":" ~ SECOND ~ TIMEZONE
}

YEAR = @{ DIGIT ~ DIGIT ~ DIGIT ~ DIGIT }
MONTH = @{ ('0'..'1') ~ DIGIT }
DAY = @{ ('0'..'3') ~ DIGIT }
HOUR = @{ ('0'..'2') ~ DIGIT  }
MINUTE = @{ ('0'..'5') ~ DIGIT }
SECOND = @{ ('0'..'5') ~ DIGIT }
TIMEZONE = @{ "Z" | ("+" | "-") ~ HOUR ~ ":" ~ MINUTE }

DIGIT = _{ '0'..'9' }
ASCII_NONZERO_DIGIT = _{ '1'..'9' }

CCVER_LOG_ENTRY = {
    "name="
    ~ SCOPE
    ~ "branch="
    ~ BRANCH
    ~ "commit="
    ~ COMMIT_HASHLINE
    ~ "commit-time="
    ~ ISO8601_DATE
    ~ "dec="
    ~ DECORATIONS_LINE?
    ~ "parent="
    ~ PARENT_HASHLINE
    ~ "sub="
    ~ SUBJECT
    ~ "trailers="
    ~ FOOTER_SECTION
}

CCVER_LOG = {CCVER_LOG_ENTRY+ ~ EOI}

BRANCH = _{ SCOPE }


CCVER_VERSION = {V_PREFIX ~ VERSION_NUMBER ~ "." ~ VERSION_NUMBER ~ "." ~ VERSION_NUMBER ~ PRE_TAG? }
VERSION_NUMBER = @{ NUMBER+ }
PRE_TAG = ${
        RC_PRE_TAG
        | BETA_PRE_TAG
        | ALPHA_PRE_TAG
        | BUILD_PRE_TAG
        | NAMED_PRE_TAG
        | SHA_PRE_TAG
}





RC_PRE_TAG = {("-rc." ~ VERSION_NUMBER)}
BETA_PRE_TAG = {("-beta." ~ VERSION_NUMBER)}
ALPHA_PRE_TAG = {("-alpha." ~ VERSION_NUMBER)}
BUILD_PRE_TAG = {("-build." ~ VERSION_NUMBER)}
NAMED_PRE_TAG = {("-" ~ NAME ~ "." ~ VERSION_NUMBER)}
SHA_PRE_TAG = { "+" ~ (SHA | SHORT_SHA) }


CCVER_VERSION_FORMAT = { 
    V_PREFIX 
    ~ VERSION_NUMBER_FORMAT 
    ~ "." 
    ~ VERSION_NUMBER_FORMAT 
    ~ "." 
    ~ VERSION_NUMBER_FORMAT 
    ~ PRE_TAG_FORMAT?
}

V_PREFIX = @{ "v"? }

RC_PRE_TAG_FORMAT = {("-rc." ~ VERSION_NUMBER_FORMAT)}
BETA_PRE_TAG_FORMAT = {("-beta." ~ VERSION_NUMBER_FORMAT)}
ALPHA_PRE_TAG_FORMAT = {("-alpha." ~ VERSION_NUMBER_FORMAT)}
BUILD_PRE_TAG_FORMAT = {("-build." ~ VERSION_NUMBER_FORMAT)}
NAMED_PRE_TAG_FORMAT = {("-" ~ NAME ~ "." ~ VERSION_NUMBER_FORMAT)}
SHA_PRE_TAG_FORMAT = { "+" ~ (SHA_FORMAT | SHORT_SHA_FORMAT) }
SHA_FORMAT = @{ "<sha>" }
SHORT_SHA_FORMAT = @{ "<short-sha>"}


PRE_TAG_FORMAT = {
        RC_PRE_TAG_FORMAT
        | BETA_PRE_TAG_FORMAT
        | ALPHA_PRE_TAG_FORMAT
        | BUILD_PRE_TAG_FORMAT
        | NAMED_PRE_TAG_FORMAT
        | SHA_PRE_TAG_FORMAT
}

VERSION_NUMBER_FORMAT = {
    SEMANTIC_VERSION_FORMAT
    | CALENDAR_VERSION_FORMAT
    | SHA_VERSION_FORMAT
}

SHA_VERSION_FORMAT = { SHA_FORMAT | SHORT_SHA_FORMAT }

SEMANTIC_VERSION_FORMAT = @{ "CC" }
CALENDAR_VERSION_FORMAT = { CALENDAR_VERSION_FORMAT_SEGMENT+ }
CALENDAR_VERSION_FORMAT_SEGMENT = @{
    YEAR2_SEGMENT_FORMAT
    | YEAR4_SEGMENT_FORMAT
    | EPOCH_SEGMENT_FORMAT
    | DOY_SEGMENT_FORMAT
    | MONTH_SEGMENT_FORMAT
    | DAY_SEGMENT_FORMAT
    | HOUR_SEGMENT_FORMAT
    | MINUTE_SEGMENT_FORMAT
    | SECOND_SEGMENT_FORMAT
}

YEAR2_SEGMENT_FORMAT = @{ "YY" }
YEAR4_SEGMENT_FORMAT = @{ "YYYY" }
EPOCH_SEGMENT_FORMAT = @{ "E" }
MONTH_SEGMENT_FORMAT = @{ "MM" }
DAY_SEGMENT_FORMAT = @{ "DD" }
DOY_SEGMENT_FORMAT = @{ "DDD" }
HOUR_SEGMENT_FORMAT = @{ "hh" }
MINUTE_SEGMENT_FORMAT = @{ "mm" }
SECOND_SEGMENT_FORMAT = @{ "ss" }

