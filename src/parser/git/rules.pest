WHITESPACE = _{ " " }

SHORT_SHA = { ASCII_HEX_DIGIT{7} }

ALPHANUMERIC_DASH_UNDERSCORE = _{ LETTER | NUMBER | "-" | "_" }
ALPHANUMERIC_DASH_UNDERSCORE_SLASH = { ALPHANUMERIC_DASH_UNDERSCORE | "/" }
ALPHANUMERIC_DOT_DASH_UNDERSCORE = { ALPHANUMERIC_DASH_UNDERSCORE | "." }

BRANCH_NAME = { ALPHANUMERIC_DASH_UNDERSCORE_SLASH+ }
TAG_NAME = { ALPHANUMERIC_DASH_UNDERSCORE_SLASH+ }

HEAD_INFO = { "HEAD ->"  ~ BRANCH_NAME }
TAG_INFO = { "tag: " ~ TAG_NAME }
INFO_ENTRY = { HEAD_INFO | TAG_INFO | BRANCH_NAME }
COMMIT_INFO = { "(" ~ (INFO_ENTRY ~ ", ")* ~ INFO_ENTRY ~ ")" }
POSITION = { "HEAD@{" ~ ASCII_DIGIT+ ~ "}:" }
REFLOG_LINE = { SHORT_SHA  ~ COMMIT_INFO?  ~ POSITION}
REFLOG = { (REFLOG_LINE ~ LINE_SEPARATOR)+ }




LOG = {(LOG_ENTRY ~ NEWLINE)* ~ LOG_ENTRY ~ EOI}

LOG_ENTRY = {
    COMMIT_LOGLINE?
    ~ TREE_LOGLINE?
    ~ PARENT_LOGLINE*
    ~ MERGE_LOGLINE?
    ~ AUTHOR_LOGLINE 
    ~ COMMITTER_LOGLINE?
    ~ DATE_LOGLINE? 
    ~ NEWLINE
    ~ #message = MESSAGE
    ~ NEWLINE
}

MERGE_LOGLINE = _{ "Merge: " ~ #merge_target = SHORT_SHA ~ #merge_source = SHORT_SHA ~ NEWLINE?}

COMMIT_LOGLINE = _{ "commit" ~ #commit_sha = SHA ~ COMMIT_INFO? ~ NEWLINE?}
SHA = @{ ASCII_HEX_DIGIT{40} }
AUTHOR_LOGLINE = _{  AUTHOR_INFO ~ NEWLINE } 
AUTHOR_INFO = {("Author: " | "author ") ~ #author_name = NAME ~ "<" ~ #author_email = EMAIL ~ ">" ~ #author_time = EPOC_TIME_W_TZ?}
COMMITTER_LOGLINE = _{  COMMITTER_INFO ~ NEWLINE }
COMMITTER_INFO = {("Committer: " | "committer ") ~ #committer_name = NAME ~ "<" ~ #committer_email = EMAIL ~ ">" ~ #committer_time = EPOC_TIME_W_TZ?}
EPOC_TIME_W_TZ = @{ NUMBER{10} ~ " " ~ TIMEZONE }
NAME = @{ ALPHANUMERIC_DOT_DASH_UNDERSCORE+ }
EMAIL = @{ ALPHANUMERIC_DOT_DASH_UNDERSCORE+ ~ "@" ~ ALPHANUMERIC_DOT_DASH_UNDERSCORE+ }
MESSAGE = { PRINTABLE* }
PRINTABLE = _{ NUMBER | LETTER | PUNCTUATION | SYMBOL | SEPARATOR }
DATE_LABEL = _{ "Date:" }
DATE_LOGLINE = _{ DATE_LABEL ~ #date_string = DATE_STRING ~ NEWLINE? }
DATE_STRING = { DAYOFWEEK ~ MONTHOFYEAR ~ DAYOFMONTH ~ TIME ~ YEAR ~ TIMEZONE}
DAYOFWEEK = _{ "Fri" | "Sat" | "Sun" | "Mon" | "Tue" | "Wed" | "Thu" }
MONTHOFYEAR = _{ "Jan" | "Feb" | "Mar" | "Apr" | "May" | "Jun" | "Jul" | "Aug" | "Sep" | "Oct" | "Nov" | "Dec" }
DAYOFMONTH = _{ NUMBER{1,2} }
TIME = _{ NUMBER{1,2} ~ ":" ~ NUMBER{1,2} ~ ":" ~ NUMBER{1,2} } 
YEAR = _{ NUMBER{1,4} }
TIMEZONE = @{ ("+" | "-") ~ NUMBER{4} }


TREE_LOGLINE = _{ "tree" ~ #tree_sha = SHA ~ NEWLINE?}
PARENT_LOGLINE = _{ "parent" ~ #parent_sha = SHA ~ NEWLINE?}